COHORT=["cohorta", "cohortb"]
REGION=["region1", "region2"]

rule all:
    input:
        bed="results/merged.bed",
        bim="results/merged.bim",
        fam="results/merged.fam",
        varmet="results/variant_metrics.rds",
        var_pruned="results/ld_pruned.rds",

rule create_variant_exclude:
    input:
        info="data/test.info.gz",
        tbi="data/test.info.gz.tbi",
    output: "results/variant_exclude.txt"
    params:
        avg_call=0.95,
        rsq_common=0.5,
        maf_thresh=0.05,
        rsq_rare=0.8,
    shell:
        """
        bcftools query -f '%ID\\n' -i \
                'INFO/AVG_CS < {params.avg_call} || \
                INFO/R2 < {params.rsq_common} || \
                (INFO/MAF < {params.maf_thresh} && INFO/R2 < {params.rsq_rare})' \
            {input.info} > \
            {output}
        """

# This rule wil **not** execute properly with the current input, but it can be
# imported in another Snakemake workflow. It's there as a placeholder. This rule
# creates the same exclusion list as create_variant_exclude but takes a gzipped
# tab-delimited file as input instead of a VCF. It requires MAF in the 5th
# column, AVG_CALL in the 6th column, and RSQ in the 7th column.
rule create_variant_exclude_delim:
    input: "data/test.info.gz",
    output: "results/__variant_exclude_delim__.txt"
    params:
        avg_call=0.95,
        rsq_common=0.5,
        maf_thresh=0.05,
        rsq_rare=0.8,
    shell:
        """
        gunzip -c {input} | perl -lane 'print $F[0] if \
                $F[5] < {params.avg_call} | \
                $F[6] < {params.rsq_common} | \
                ($F[4] < {params.maf_thresh} & $F[6] < {params.rsq_rare})' > \
            {output}
        """

rule filter_vcf:
    input:
        vcf="data/{cohort}_{region}.vcf.gz",
        tbi="data/{cohort}_{region}.vcf.gz.tbi",
        exclude="results/variant_exclude.txt",
    output:
        vcf="results/{cohort}_{region}_filtered.vcf.gz",
        tbi="results/{cohort}_{region}_filtered.vcf.gz.tbi",
    params:
        extra_bcftools_opts="--no-version",
    shell:
        """
        bcftools view {params.extra_bcftools_opts} -e 'ID=@{input.exclude}' {input.vcf} | \
            bgzip > \
            {output.vcf}
        tabix {output.vcf}
        """

rule concat_vcf:
    input: expand("results/{{cohort}}_{region}_filtered.vcf.gz", region=REGION)
    output:
        vcf="results/{cohort}.vcf.gz",
        tbi="results/{cohort}.vcf.gz.tbi",
    params:
        extra_bcftools_opts="--no-version",
    shell:
        """
        bcftools concat {params.extra_bcftools_opts} {input} | bgzip -c > {output.vcf}
        tabix {output.vcf}
        """ 

rule merge_vcf:
    input:
        vcf=expand("results/{cohort}.vcf.gz", cohort=COHORT),
        tbi=expand("results/{cohort}.vcf.gz.tbi", cohort=COHORT),
    output:
        vcf="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi"
    params:
        extra_bcftools_opts="--no-version",
    shell:
        """
        bcftools merge {params.extra_bcftools_opts} {input.vcf} | bgzip -c > {output.vcf}
        tabix {output.vcf}
        """

rule vcf_to_plink:
    input:
        vcf="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi",
    output:
        bed="results/merged.bed",
        bim="results/merged.bim",
        fam="results/merged.fam",
    params:
        plink_exe="plink1.9",
    shell:
        """
        {params.plink_exe} --vcf {input.vcf} --make-bed --out results/merged
        """

rule convert_to_gds:
    input:
        vcf_fn="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi",
    output:
        out_fn="results/combined.gds",
    script: "scripts/convert_to_gds.R"

rule seqMissing:
    input:
        gds_fn="results/combined.gds"
    output: "results/var_missing.rds"
    params:
        per_variant=True,
    threads: 2
    script: "scripts/seqMissing.R"

rule seqAlleleFreq:
    input:
        gds_fn="results/combined.gds"
    output: "results/maf.rds"
    threads: 2
    params:
        minor=True
    script: "scripts/seqAlleleFreq.R"

rule heterozygosity:
    input:
        gds_fn="results/combined.gds"
    output: "results/var_htz.rds"
    threads: 2
    params:
        margin="by.variant",
    script: "scripts/heterozygosity.R"

rule hwe:
    input:
        gds_fn="results/combined.gds"
    output: "results/hwe.rds"
    params:
        seed=51,
        parallel=False,
    script: "scripts/hwe.R"

rule get_var_data:
    input:
        gds_fn="results/combined.gds"
    output: "results/var_data.rds"
    threads: 2
    params:
        build="hg38",
    script: "scripts/get_var_data.R"

rule combine_varmet:
    input:
        "results/var_data.rds",
        "results/var_missing.rds",
        "results/maf.rds",
        "results/var_htz.rds",
        "results/hwe.rds",
    output: "results/variant_metrics.rds"
    script: "scripts/combine_varmet.R"

rule parse_sexcheck:
    script: "scripts/parse_sexcheck.R"

rule snpgdsLDpruning:
    input:
        gds_fn="results/combined.gds"
    output: "results/ld_pruned.rds"
    threads: 2
    params:
        method="corr",
        ld_threshold=0.2,
        seed=51,
    script: "scripts/ld_prune.R"

rule snpgdsIBDKING:
    input:
        gds_fn="results/combined.gds"
    output: "results/ibd_king.rds"
    params:
        type="KING-robust"
    script: "scripts/king.R"