COHORT=["cohorta", "cohortb"]
REGION=["region1", "region2"]

rule all:
    input:
        vcf_merged="results/merged.vcf.gz",
        tbi_merged="results/merged.vcf.gz.tbi",
        gds="results/combined.gds",
        bed="results/merged.bed",
        bim="results/merged.bim",
        fam="results/merged.fam",
        var_ms="results/var_missing.rds",

rule create_variant_exclude:
    input:
        info="data/test.info.gz",
        tbi="data/test.info.gz.tbi",
    output: "results/variant_exclude.txt"
    params:
        avg_call=0.95,
        rsq_common=0.5,
        maf_thresh=0.05,
        rsq_rare=0.8,
    shell:
        """
        bcftools query -f '%ID\\n' -i \
                'INFO/AVG_CS < {params.avg_call} || \
                INFO/R2 < {params.rsq_common} || \
                (INFO/MAF < {params.maf_thresh} && INFO/R2 < {params.rsq_rare})' \
            {input.info} > \
            {output}
        """

# This rule wil **not** execute properly with the current input, but it can be
# imported in another Snakemake workflow. It's there as a placeholder. This rule
# creates the same exclusion list as create_variant_exclude but takes a gzipped
# tab-delimited file as input instead of a VCF. It requires MAF in the 5th
# column, AVG_CALL in the 6th column, and RSQ in the 7th column.
rule __create_variant_exclude_delim__:
    input: "data/test.info.gz",
    output: "results/__variant_exclude_delim__.txt"
    params:
        avg_call=0.95,
        rsq_common=0.5,
        maf_thresh=0.05,
        rsq_rare=0.8,
    shell:
        """
        gunzip -c {input} | perl -lane 'print $F[0] if \
                $F[5] < {params.avg_call} | \
                $F[6] < {params.rsq_common} | \
                ($F[4] < {params.maf_thresh} & $F[6] < {params.rsq_rare})' > \
            {output}
        """

rule filter_vcf:
    input:
        vcf="data/{cohort}_{region}.vcf.gz",
        tbi="data/{cohort}_{region}.vcf.gz.tbi",
        exclude="results/variant_exclude.txt",
    output:
        vcf="results/{cohort}_{region}_filtered.vcf.gz",
        tbi="results/{cohort}_{region}_filtered.vcf.gz.tbi",
    shell:
        """
        bcftools view -e 'ID=@{input.exclude}' {input.vcf} | \
            bgzip > \
            {output.vcf}
        tabix {output.vcf}
        """

rule concat_vcf:
    input: expand("results/{{cohort}}_{region}_filtered.vcf.gz", region=REGION)
    output:
        vcf="results/{cohort}.vcf.gz",
        tbi="results/{cohort}.vcf.gz.tbi",
    shell:
        """
        bcftools concat {input} | bgzip -c > {output.vcf}
        tabix {output.vcf}
        """ 

rule merge_vcf:
    input:
        vcf=expand("results/{cohort}.vcf.gz", cohort=COHORT),
        tbi=expand("results/{cohort}.vcf.gz.tbi", cohort=COHORT),
    output:
        vcf="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi"
    shell:
        """
        bcftools merge {input.vcf} | bgzip -c > {output.vcf}
        tabix {output.vcf}
        """

rule vcf_to_plink:
    input:
        vcf="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi",
    output:
        bed="results/merged.bed",
        bim="results/merged.bim",
        fam="results/merged.fam",
    params:
        plink_exe="plink",
    shell:
        """
        {params.plink_exe} --vcf {input.vcf} --make-bed --out results/merged
        """

rule convert_to_gds:
    input:
        vcf_fn="results/merged.vcf.gz",
        tbi="results/merged.vcf.gz.tbi",
    output:
        out_fn="results/combined.gds",
    script: "scripts/convert_to_gds.R"

rule var_ms:
    input: "results/combined.gds"
    output: "results/var_missing.rds"
    threads: 2
    script: "scripts/var_ms.R"

rule var_maf:
    input: "results/combined.gds"
    output: "results/maf.rds"
    threads: 2
    params:
        minor=True
    script: "scripts/maf.R"