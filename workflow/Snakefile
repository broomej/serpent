configfile: "config.yaml"
container: "docker://broome/genetics-tools:ACT-ROSMAP-NACC-meta"

rule all:
    input:
        expand("results/{cohort}_{region}_filtered.vcf.gz",
               cohort=["cohorta", "cohortb"],
               region=["region1", "region2"])

rule create_variant_exclude:
    input:
        info="data/test.info.gz",
        tbi="data/test.info.gz.tbi",
    output: "results/variant_exclude.txt"
    params:
        avg_call=config["params"]["impt_filter"]["avg_call"],
        rsq_common=config["params"]["impt_filter"]["rsq_common"],
        maf_thresh=config["params"]["impt_filter"]["maf_thresh"],
        rsq_rare=config["params"]["impt_filter"]["rsq_rare"],
    shell:
        """
        bcftools query -f '%ID\\n' -i \
                'INFO/AVG_CS < {params.avg_call} || \
                INFO/R2 < {params.rsq_common} || \
                (INFO/MAF < {params.maf_thresh} && INFO/R2 < {params.rsq_rare})' \
            {input.info} > \
            {output}
        """

rule filter_vcf:
    input:
        vcf="data/{cohort}_{region}.vcf.gz",
        tbi="data/{cohort}_{region}.vcf.gz.tbi",
        exclude="results/variant_exclude.txt",
    output:
        "results/{cohort}_{region}_filtered.vcf.gz"
    shell:
        """
        bcftools view -e 'ID=@{input.exclude}' {input.vcf} | \
            bgzip > \
            {output}
        tabix {output}
        """